
ALTER PROCEDURE [dbo].[TrnConversionRs1_ApprovalWorkflow]
    @Doc_ID INT,
    @Action NVARCHAR(50),
    @Comment NVARCHAR(255),
    @Approval_Level INT,
    @UserID NVARCHAR(255)
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE @Created_By INT;

    IF EXISTS (SELECT * FROM Trn_Document WHERE Doc_ID = @Doc_ID)
    BEGIN
        DECLARE @Plant_ID INT, @Movement_ID INT;

        SELECT 
            @Plant_ID = Plant_ID, 
            @Movement_ID = Movement_ID,
            @Created_By = Created_By
        FROM Trn_Document
        WHERE Doc_ID = @Doc_ID;

        IF @Movement_ID = 7
        BEGIN
            IF EXISTS (
                SELECT *
                FROM Trn_Approval_History
                WHERE Doc_ID = @Doc_ID AND Approver_Status = 'Pending'
            )
            BEGIN
                -- Update current approval status
                IF @Action IN ('Approved', 'Rejected', 'Under Query')
                BEGIN
                    UPDATE Trn_Approval_History
                    SET Approver_Status = @Action,
                        Approver_Comment = @Comment,
                        Approver_ID = @UserID,
                        Modified_By = @UserID,
                        Modified_On = GETDATE()
                    WHERE Doc_ID = @Doc_ID AND Approver_Status = 'Pending';
                END

                -- Approval flow: 3 -> 5 -> 6 -> 8 -> 7
                IF @Action = 'Approved'
                BEGIN
                    DECLARE @NextLevel INT;

                    IF @Approval_Level = 3 SET @NextLevel = 5;
                    ELSE IF @Approval_Level = 5 SET @NextLevel = 6;
                    ELSE IF @Approval_Level = 6 SET @NextLevel = 8;
                    ELSE IF @Approval_Level = 8 SET @NextLevel = 7;

                    IF @Approval_Level IN (3, 5, 6, 8)
                    BEGIN
                        INSERT INTO Trn_Approval_History (
                            Doc_ID, Plant_ID, Movement_ID, Approver_Status, Approval_Level,
                            Created_By, Created_On
                        )
                        VALUES (
                            @Doc_ID, @Plant_ID, @Movement_ID, 'Pending', @NextLevel,
                            @Created_By, GETDATE()
                        );
                    END
                    ELSE IF @Approval_Level = 7
                    BEGIN
                        -- Final level - close the process
                        UPDATE Trn_Document
                        SET Approval_Status = 'Closed'
                        WHERE Doc_ID = @Doc_ID;

                        UPDATE Trn_SapTransfer_Records
                        SET Approval_Status = 'Closed',
                            Modified_By = @UserID,
                            Modified_On = GETDATE()
                        WHERE Doc_ID = @Doc_ID;
                    END
                END
                ELSE IF @Action IN ('Rejected', 'Under Query')
                BEGIN
                    UPDATE Trn_Document
                    SET Approval_Status = @Action
                    WHERE Doc_ID = @Doc_ID;

                    UPDATE Trn_SapTransfer_Records
                    SET Approval_Status = @Action,
                        Modified_By = @UserID,
                        Modified_On = GETDATE()
                    WHERE Doc_ID = @Doc_ID;
                END
            END
        END
    END
END
